%{
   // C decls go here
#include <stdio.h>
#include <string.h>

#include "eprintf.hpp"
#include "rational.hpp"
#include "tree.hpp"
#include "dumptree.hpp"
#include "lex.yy.hpp"
#include "safe_strdup.hpp"

int remsleep = 0;
void yyerror(const char *s);

Tree *copytree(Tree *tree) {
   Tree *ret = NULL;

   if (tree) {
      ret = (Tree *)malloc(sizeof(Tree));
      memcpy(ret, tree, sizeof(Tree));
   }

   return ret;
}

#define DEBUG

Tree *programtree;

void fixup_nested_if(Tree *t, Tree *n = NULL);

%}

%union {
   Tree *u;
}

%type <u> program
%type <u> line
%type <u> linelist

%type <u> name
%type <u> efad

%type <u> arraydim
%type <u> arraydimlist

%type <u> constant
%type <u> negconstant
%type <u> posconstant
%type <u> signedconstant

%type <u> dconst
%type <u> iconst
%type <u> rconst
%type <u> sconst
%type <u> vconst

%type <u> expression
%type <u> exprlist

%type <u> label
%type <u> labellist

%type <u> cstmt
%type <u> istmt
%type <u> stmtlist

%type <u> inputstmt
%type <u> readstmt
%type <u> restorestmt
%type <u> datastmt
%type <u> datalist

%type <u> printstmt
%type <u> printlist

%type <u> repeatstmt
%type <u> controlstmt
%type <u> ifstmtlist
%type <u> optionbase
%type <u> dimstmt
%type <u> remstmt

%type <u> assignstmt

%type <u> relation

%type <u> defstmt
%type <u> paramlist

%locations

/* reserved words */
%token YYBASE
%token YYDATA
%token YYDEF
%token YYDIM

%token YYNAME

%token YYEND
%token YYFOR
%token YYGOSUB
%token YYGOTO
%token YYIF
%token YYTHEN
%token YYIFGOTO
%token YYIFGOSUB
%token YYIFSTMTLIST
%token YYINPUT
%token YYLET
%token YYNEXT
%token YYON
%token YYOPTION
%token YYPRINT
%token YYREAD
%token YYREM
%token YYRESTORE
%token YYRETURN
%token YYSTEP
%token YYSTOP
%token YYTO

%token YYDOUBLE
%token YYSTRING
%token YYINTEGER
%token YYRATIONAL
%token YYVOID // for testing

%token YYRELATION
%token YYASSIGN
%token YYEFAD
%token YYLVAL

%token YYIPSEP

%token YYLABEL
%token YYEOL

%left '&'
%left '+'
%left '-'
%left '*'
%left '/'
%left '^'
%right UPLUSMINUS

%%

program        : linelist
                  {
                     // mischief managed!
                     $$ = $1;
                     programtree = copytree($$);

                     fixup_nested_if($$);
                  }
               ;

linelist       : line
                  {
                     $$ = $1;
                  }
               | linelist line
                  {
                     $$ = $1;
                     Tree *node = $$;
                     while (node->next) {
                        node = node->next;
                     }
                     node->next = copytree($2);
                  }
               ;

line           : YYEOL
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYREM;
                  }
               | stmtlist YYEOL
                  {
                     $$ = $1;
                     $$->label = NULL;
                  }
               | label stmtlist YYEOL
                  {
                     $$ = $2;
                     $$->label = strdup(V_AS_S($1->value));
                  }
               | remstmt error YYEOL
                  {
                     $$ = $1;
                     yyerrok;
                     remsleep = 0;
                  }
               | label remstmt error YYEOL
                  {
                     $$ = $2;
                     $$->label = strdup(V_AS_S($1->value));
                     yyerrok;
                     remsleep = 0;
                  }
               ;

remstmt        : YYREM
                  {
                     remsleep = 1;
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYREM;
                  }
               ;

stmtlist       : cstmt ':' stmtlist
                  {
                     $$ = $1;
                     $$->next = copytree($3);
                  }
               | cstmt
                  {
                     $$ = $1;
                  }
               | istmt
                  {
                     $$ = $1;
                  }
               ;

istmt          : ifstmtlist
                  {
                     $$=$1;
                  }
               ;

cstmt          : assignstmt
                  {
                     $$ = $1;
                  }
               | controlstmt
                  {
                     $$ = $1;
                  }
               | datastmt
                  {
                     $$ = $1;
                  }
               | repeatstmt
                  {
                     $$ = $1;
                  }
               | printstmt
                  {
                     $$ = $1;
                  }
               | inputstmt
                  {
                     $$ = $1;
                  }
               | readstmt
                  {
                     $$ = $1;
                  }
               | restorestmt
                  {
                     $$ = $1;
                  }
               | optionbase
                  {
                     $$ = $1;
                  }
               | dimstmt
                  {
                     $$ = $1;
                  }
               | defstmt
                  {
                     $$ = $1;
                  }
               ;

defstmt        : YYDEF name '(' paramlist ')' expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYDEF;
                     $$->value.base() = strdup(V_AS_S($2->value));
                     $$->left = copytree($4);
                     $$->right = copytree($6);
                  }
               | YYDEF name '(' paramlist ')' '=' expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYDEF;
                     $$->value.base() = strdup(V_AS_S($2->value));
                     $$->left = copytree($4);
                     $$->right = copytree($7);
                  }
               | YYDEF name '(' ')' expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYDEF;
                     $$->value.base() = strdup(V_AS_S($2->value));
                     $$->left = NULL;
                     $$->right = copytree($5);
                  }
               | YYDEF name '(' ')' '=' expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYDEF;
                     $$->value.base() = strdup(V_AS_S($2->value));
                     $$->left = NULL;
                     $$->right = copytree($6);
                  }
               ;

paramlist      : paramlist YYIPSEP name
                  {
                     $$ = $1;
                     Tree *node = $$;
                     while (node->middle) {
                        node = node->middle;
                     }
                     node->middle = copytree($3);
                  }
               | name
                  {
                     $$ = $1;
                  }
               ;

optionbase     : YYOPTION YYBASE iconst
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYBASE;
                     $$->value.base() = V_AS_I($3->value);
                  }
               ;

dimstmt        : YYDIM arraydimlist
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYDIM;
                     $$->right = copytree($2);
                  }
               ;

arraydimlist   : arraydimlist YYIPSEP arraydim
                  {
                     $$ = $1;
                     Tree *node = $$;
                     while (node->middle) {
                        node = node->middle;
                     }
                     node->middle = copytree($3);
                  }
               | arraydim
                  {
                     $$ = $1;
                  }
               ;

datastmt       : YYDATA datalist
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYDATA;
                     $$->right = copytree($2);
                  }
               ;

restorestmt    : YYRESTORE
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYRESTORE;
                  }
               ;

readstmt       : YYREAD exprlist
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYREAD;
                     $$->right = copytree($2);
                  }
               ;

printstmt      : YYPRINT printlist
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYPRINT;
                     if ($2->op != YYEOF) {
                        $$->middle = copytree($2);
                     }
                  }
               ;

ifstmtlist     : YYIF expression relation expression YYTHEN stmtlist
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYIFSTMTLIST;
                     $$->value.base() = strdup(V_AS_S($3->value));
                     $$->left = copytree($2);
                     $$->middle = copytree($6);
                     $$->right = copytree($4);
                  }
               ;

controlstmt    : YYGOTO label
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYGOTO;
                     $$->value.base() = strdup(V_AS_S($2->value));
                  }
               | YYGOSUB label
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYGOSUB;
                     $$->value.base() = strdup(V_AS_S($2->value));
                  }
               | YYIF expression relation expression YYTHEN label
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYIFGOTO;
                     $$->value.base() = strdup(V_AS_S($3->value));
                     $$->left = copytree($2);
                     $$->middle = copytree($6);
                     $$->right = copytree($4);
                  }
               | YYIF expression relation expression YYGOTO label
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYIFGOTO;
                     $$->value.base() = strdup(V_AS_S($3->value));
                     $$->left = copytree($2);
                     $$->middle = copytree($6);
                     $$->right = copytree($4);
                  }
               | YYIF expression relation expression YYGOSUB label
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYIFGOSUB;
                     $$->value.base() = strdup(V_AS_S($3->value));
                     $$->left = copytree($2);
                     $$->middle = copytree($6);
                     $$->right = copytree($4);
                  }
               | YYON expression YYGOTO labellist
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYON;
                     $$->value.base() = 0;
                     $$->left = copytree($2);
                     $$->right = copytree($4);
                  }
               | YYON expression YYGOSUB labellist
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYON;
                     $$->value.base() = 1;
                     $$->left = copytree($2);
                     $$->right = copytree($4);
                  }
               | YYRETURN
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYRETURN;
                  }
               | YYSTOP
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYSTOP;
                  }
               | YYEND
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYEND;
                  }
               ;

relation       : YYRELATION
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYRELATION;
                     $$->value.base() = strdup(yytext);
                  }
               | '<'
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYRELATION;
                     $$->value.base() = strdup("<");
                  }
               | '>'
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYRELATION;
                     $$->value.base() = strdup(">");
                  }
               | '='
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYRELATION;
                     $$->value.base() = strdup("=");
                  }
               ;

repeatstmt     : YYFOR name '=' expression YYTO expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYFOR;
                     $$->value.base() = strdup(V_AS_S($2->value));
                     $$->left = copytree($4);
                     $$->right = copytree($6);
                     $$->middle = NULL;
                  }
               | YYFOR name '=' expression YYTO expression YYSTEP expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYFOR;
                     $$->value.base() = strdup(V_AS_S($2->value));
                     $$->left = copytree($4);
                     $$->right = copytree($6);
                     $$->middle = copytree($8);
                  }
               | YYNEXT name
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYNEXT;
                     $$->value.base() = strdup(V_AS_S($2->value));
                  }
               ;

label          : name
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYLABEL;
                     $$->value.base() = strdup(V_AS_S($1->value));
                  }
               | YYINTEGER
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYLABEL;
                     $$->value.base() = strdup(yytext);
                  }
               ;

labellist      : label
                  {
                     $$ = $1;
                  }
               | label YYIPSEP labellist
                  {
                     $$ = $1;
                     $$->middle = copytree($3);
                  }
               ;

name           : YYNAME
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYNAME;
                     $$->value.base() = strdup(yytext);
                  }
               ;

arraydim       : name '(' iconst ')'
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYDIM;
                     $$->value.base() = strdup(V_AS_S($1->value));
                     $$->left = copytree($3);
                     $$->right = NULL;
                  }
               | name '(' iconst YYIPSEP iconst ')'
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYDIM;
                     $$->value.base() = strdup(V_AS_S($1->value));
                     $$->left = copytree($3);
                     $$->right = copytree($5);
                  }
               ;

constant       : dconst
                  {
                     $$ = $1;
                  }
               | iconst
                  {
                     $$ = $1;
                  }
               | rconst
                  {
                     $$ = $1;
                  }
               | sconst
                  {
                     $$ = $1;
                  }
               | vconst
                  {
                     $$ = $1;
                  }
               ;

negconstant    : '-' dconst
                  {
                     $$ = $2;
                     $$->value.base() = - V_AS_D($$->value);
                  }
               | '-' iconst
                  {
                     $$ = $2;
                     $$->value.base() = - V_AS_I($$->value);
                  }
               | '-' rconst
                  {
                     $$ = $2;
                     Rational *rval = new Rational(- *(V_AS_R($$->value)));
                     $$->value.vacate();
                     $$->value.base() = rval;
                  }
               ;

posconstant    : '+' dconst
                  {
                     $$ = $2;
                  }
               | '+' iconst
                  {
                     $$ = $2;
                  }
               | '+' rconst
                  {
                     $$ = $2;
                  }
               ;

datalist       : signedconstant YYIPSEP datalist
                  {
                     $$ = $1;
                     $$->middle = copytree($3);
                  }
               | signedconstant
                  {
                     $$ = $1;
                  }
               ;

signedconstant : constant
                  {
                     $$ = $1;
                  }
               | negconstant
                  {
                     $$ = $1;
                  }
               | posconstant
                  {
                     $$ = $1;
                  }
               ;

expression     : '+' expression %prec UPLUSMINUS
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = '+';
                     $$->left = NULL;
                     $$->right = copytree($2);
                  }
               | '-' expression %prec UPLUSMINUS
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = '-';
                     $$->left = NULL;
                     $$->right = copytree($2);
                  }
               | expression '&' expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = '&';
                     $$->left = copytree($1);
                     $$->right = copytree($3);
                  }
               | expression '+' expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = '+';
                     $$->left = copytree($1);
                     $$->right = copytree($3);
                  }
               | expression '-' expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = '-';
                     $$->left = copytree($1);
                     $$->right = copytree($3);
                  }
               | expression '*' expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = '*';
                     $$->left = copytree($1);
                     $$->right = copytree($3);
                  }
               | expression '/' expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = '/';
                     $$->left = copytree($1);
                     $$->right = copytree($3);
                  }
               | expression '^' expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = '^';
                     $$->left = copytree($1);
                     $$->right = copytree($3);
                  }
               | '(' expression ')'
                  {
                     $$ = $2;
                  }
               | efad
                  {
                     $$ = $1;
                  }
               | constant
                  {
                     $$ = $1;
                  }
               ;

efad           : name
                  {
                     $$ = $1;
                     $$->op = YYEFAD;
                  }
               | name '(' ')'
                  {
                     $$ = $1;
                     $$->op = YYEFAD;
                  }
               | name '(' exprlist ')'
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYEFAD;
                     $$->value.base() = strdup(V_AS_S($1->value));
                     // unnecessary // $$->left = copytree($1);
                     $$->right = copytree($3);
                  }
               ;

exprlist       : exprlist YYIPSEP expression
                  {
                     $$ = $1;
                     Tree *node = $$;
                     while (node->middle) {
                        node = node->middle;
                     }
                     node->middle = copytree($3);
                  }
               | expression
                  {
                     $$ = $1;
                  }
               ;

printlist      : expression YYIPSEP printlist
                  {
                     $$ = $1;
                     if ($3->op == YYEOF) {
                        $$->middle = NULL;
                     }
                     else {
                        $$->middle = copytree($3);
                     }
                  }
               | expression
                  {
                     $$ = $1;
                  }
               | %empty
                  {
                  }
               ;

inputstmt      : YYINPUT exprlist
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYINPUT;
                     $$->right = copytree($2);
                  }
               ;

assignstmt     : efad '=' expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYASSIGN;
                     $$->value.base() = V_AS_S($1->value);
                     $$->left = copytree($1);
                     $$->left->op = YYLVAL;
                     $$->right = copytree($3);
                  }
               | YYLET efad '=' expression
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYASSIGN;
                     $$->value.base() = V_AS_S($2->value);
                     $$->left = copytree($2);
                     $$->left->op = YYLVAL;
                     $$->right = copytree($4);
                  }
               ;

dconst         : YYDOUBLE
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYDOUBLE;
                     $$->value.base() = atof(yytext);
                  }
               ;

vconst         : YYVOID
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYVOID;
                     $$->value.base() = (char) 0;
                  }
               ;

iconst         : YYINTEGER
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYINTEGER;
                     $$->value.base() = atoll(yytext);
                  }
               ;

rconst         : YYRATIONAL
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYRATIONAL;
                     $$->value.base() = new Rational(yytext);
                  }
               ;

sconst         : YYSTRING
                  {
                     $$->line = yylineno;
                     $$->col = yyloc.first_column;
                     $$->op = YYSTRING;
                     char *buffer = (char *)alloca(strlen(yytext + 1));
                     char *q = buffer;
                     char *s = yytext + 1;
                     while(*s) {
                        if (*s == '\\') {
                           s++;
                        }
                        *q++ = *s++;
                     }
                     q[-1] = 0;
                     $$->value.base() = strdup(buffer);
                  }
               ;
%%

void fixup_nested_if(Tree *t, Tree *n) {
   while (t) {
      if (t->op == YYIFSTMTLIST) {
         fixup_nested_if(t->middle, t->next ? t->next : n);
      }
      if (t->next) {
         t = t->next;
      }
      else {
         if (n != NULL) {
            t->next = n;
         }
         t = NULL;
      }
   }
}

void yyerror(const char *s) {
   if (!remsleep) {
      if (!strcmp(s, "syntax error")) {
         eprintf("{SYNTAX ERROR} @%0:%1 ❮%2❯%n",
            yylineno, yylloc.first_column, (yytext[0] < ' ') ? "EOL" : yytext);
      }
      else {
         eprintf("{ERROR} @%0:%1 ❮%2❯ ❮%3❯%n",
            yylineno, yylloc.first_column, s, yytext);
      }
   }
}

